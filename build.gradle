buildscript {
    //dependencies
    ext.coroutinesVersion = "1.4.3"
    ext.junit5Version = "5.7.1"
    ext.kotlin_version = "1.4.32"


    //versions & naming
    ext.csenseVersionName = "0.0.49"
    ext.csenseGroupId = "csense.kotlin"
    ext.csenseArtifactId = "csense-kotlin-tests"

    //repo settings
    ext.csenseUserOrg = "csense-oss"
    ext.csenseRepoName = "maven"

    ext.csenseGithub = "csense-oss/csense-kotlin-tests"
    ext.csenseLicense = "MIT"
    ext.csenseLicenseUrl = "https://mit-license.org/"

    ext.csenseDeveloperAlias = "tvede-dk"
    ext.csenseDeveloperName = "Kasper Tvede"
    ext.csenseDeveloperEmail = "csenseoss@tvedesys.dk"

    ext.readme = "readme.md"
    ext.csenseWebsite = "https://github.com/csense-oss/csense-kotlin-tests"
    ext.csenseGit = "https://github.com/csense-oss/csense-kotlin-tests.git"
    ext.csenseArtifactDescription = "A library for writing tests with a focus on simple features (assertions)"
    ext.csenseVersionDescription = ""

}

plugins {
    id "net.linguica.maven-settings" version "0.5"
    id 'org.jetbrains.kotlin.multiplatform' version "1.4.32"
    id 'org.jetbrains.dokka' version "1.4.20"
    id "maven-publish"
}

repositories {
    mavenCentral()
}

group project.csenseGroupId
version project.csenseVersionName

kotlin {
    explicitApi = 'strict'
    jvm()
    js(BOTH) { //https://kotlinlang.org/docs/reference/whatsnew14.html#new-js-ir-backend
        browser()
        nodejs()
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependencies {
                api "org.junit.jupiter:junit-jupiter-api:$junit5Version"
                api "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
                api 'junit:junit:4.13'
                api "org.junit.vintage:junit-vintage-engine:$junit5Version"
                api "org.jetbrains.kotlin:kotlin-test-junit5:$kotlin_version"
                api "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
            }
        }

        jsMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
            }
        }
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.RequiresOptIn')
                progressiveMode = true
            }
        }
    }
}


project.group = csenseGroupId
project.version = csenseVersionName


jvmTest {
    testLogging {
        showStandardStreams = true
        events "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
    }
    useJUnitPlatform()
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

pluginManager.withPlugin("org.jetbrains.kotlin.multiplatform") {
    kotlin.targets.matching { it.platformType.name == "jvm" }.all {
        compilations.all {
            kotlinOptions.useIR = true
        }
    }

}

apply from: "$rootDir/gradle/publish.gradle"
