buildscript {
    //dependencies
    //https://github.com/Kotlin/kotlinx.coroutines
    ext.coroutinesVersion = "1.6.0"
    //https://github.com/junit-team/junit5/releases
    ext.junit5Version = "5.8.2"
    //https://github.com/junit-team/junit4/releases
    ext.junit4Version = "4.13.2"
    //https://github.com/JetBrains/kotlin/releases
    ext.kotlin_version = "1.6.10"


    //versions & naming
    ext.csenseVersionName = "0.0.53"
    ext.csenseGroupId = "csense.kotlin"
    ext.csenseArtifactId = "csense-kotlin-tests"

    //repo settings
    ext.csenseUserOrg = "csense-oss"
    ext.csenseRepoName = "maven"

    ext.csenseGithub = "csense-oss/csense-kotlin-tests"
    ext.csenseLicense = "MIT"
    ext.csenseLicenseUrl = "https://mit-license.org/"

    ext.csenseDeveloperAlias = "tvede-dk"
    ext.csenseDeveloperName = "Kasper Tvede"
    ext.csenseDeveloperEmail = "csenseoss@tvedesys.dk"

    ext.readme = "readme.md"
    ext.csenseWebsite = "https://github.com/csense-oss/csense-kotlin-tests"
    ext.csenseGit = "https://github.com/csense-oss/csense-kotlin-tests.git"
    ext.csenseArtifactDescription = "A library for writing tests with a focus on simple test features (assertions)"

}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version "$kotlin_version"
    id "maven-publish"
}

repositories {
    mavenCentral()
}

group project.csenseGroupId
version project.csenseVersionName

kotlin {
    explicitApi = 'strict'
    jvm()
    js(BOTH) { //https://kotlinlang.org/docs/reference/whatsnew14.html#new-js-ir-backend
        browser()
        nodejs()
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependencies {
                api "org.junit.jupiter:junit-jupiter-api:$junit5Version"
                api "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
                api "junit:junit:$junit4Version"
                api "org.junit.vintage:junit-vintage-engine:$junit5Version"
                api "org.jetbrains.kotlin:kotlin-test-junit5:$kotlin_version"
                api "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
            }
        }

        jsMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
            }
        }
        all {
            languageSettings {
                optIn('kotlin.RequiresOptIn')
                progressiveMode = true
            }
        }
    }
}


project.group = csenseGroupId
project.version = csenseVersionName


jvmTest {
    testLogging {
        showStandardStreams = true
        events "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
    }
    useJUnitPlatform()
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

apply from: "$rootDir/gradle/publish.gradle"
